#include <FastLED.h>

#define LED_PIN 9
#define NUM_LEDS 81
#define BRIGHTNESS 100
#define LED_TYPE WS2812B
#define COLOR_ORDER GRB

CRGB leds[NUM_LEDS];

const int WIDTH = 9;
const int HEIGHT = 9;

#define JOY_X A0
#define JOY_Y A1

const int DEADZONE_MIN = 450;
const int DEADZONE_MAX = 570;

bool stickCentered = true;

int snake[81];
int snakeLength = 3;
int direction = 1;
int nextDirection = 1;

int headRow = 4;
int headCol = 4;

int food = 40;

int XY(int x, int y) {
  if (y % 2 == 0) {
    return y * WIDTH + x;
  } else{
    return y * WIDTH + (WIDTH - 1 - x);
  }
}

void spawnFood() {
  while (true) {
    int f = random(0, 81);
    bool onSnake = false;
    
    for (int i = 0; i < snakeLength; i++) {
      if (snake[i] == f) onSnake = true;
    }

    if (!onSnake) {
      food = f;
      return;
    }
  }
}

void resetSnake() {
  snakeLength = 3;
  headRow = 4;
  headCol = 4;
  snake[0] = XY(4, 4);
  snake[1] = XY(3, 4);
  snake[2] = XY(2, 4);
  direction = 1;
  nextDirection = 1;
  stickCentered = true;
  spawnFood();
  delay(300);
}

void setup() {
  FastLED.addLeds<LED_TYPE, LED_PIN, COLOR_ORDER>(leds, NUM_LEDS);
  FastLED.setBrightness(BRIGHTNESS);

  pinMode(JOY_X, INPUT);
  pinMode(JOY_Y, INPUT);
  randomSeed(analogRead(0));

  resetSnake();
}

void readJoystick() {
  int x = analogRead(JOY_X);
  int y = analogRead(JOY_Y);

  bool inDeadzone = (x > DEADZONE_MIN && x < DEADZONE_MAX && y > DEADZONE_MIN && y < DEADZONE_MAX);

  if (inDeadzone) {
    stickCentered = true;
    return;
  }

  if (!stickCentered) return;

  int newDir = direction;

  if (x < DEADZONE_MIN) newDir = 1;
  else if (x > DEADZONE_MAX) newDir = 3;
  else if (y < DEADZONE_MIN) newDir = 0;
  else if (y > DEADZONE_MAX) newDir = 2;

  if (!((direction == 0 && newDir == 2) || (direction == 2 && newDir == 0) || (direction == 1 && newDir == 3) || (direction == 3 && newDir == 1))) {
    nextDirection = newDir;
    stickCentered = false;
  }
}

void loop() {
  readJoystick();

  direction = nextDirection;

  int dRow = 0;
  int dCol = 0;


  if (direction == 0) dRow = -1;
  if (direction == 1) dCol = 1;
  if (direction == 2) dRow = 1;;
  if (direction == 3) dCol = -1;

  int newRow = headRow + dRow;
  int newCol = headCol + dCol;

  if (newRow < 0 || newRow >= WIDTH || newCol < 0 || newCol >= HEIGHT) {
    resetSnake();
    return;
  }

  int newHead = XY(newCol, newRow);

  for (int i = 0; i < snakeLength; i++) {
    if (snake[i] == newHead) {
      resetSnake();
      return;
    }
  }

  for (int i = snakeLength; i > 0; i--) {
    snake[i] = snake[i - 1];
  }
  
  snake[0] = newHead;
  headRow = newRow;
  headCol = newCol;

  if (newHead == food) {
    snakeLength++;
    spawnFood();
  } 

  fill_solid(leds, NUM_LEDS, CRGB::Blue);

  leds[food] = CRGB::Red;
  leds[snake[0]] = CRGB::Green;
  
  for (int i = 1; i < snakeLength; i++) {
    leds[snake[i]] = CRGB::Yellow;
  }

  FastLED.show();
  delay(300);
}
